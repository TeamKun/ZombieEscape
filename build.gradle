import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "6.1.0"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

group = 'lab.kunmc.net'
version = '1,1'

repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://papermc.io/repo/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        url = "https://repo.dmulloy2.net/repository/public/"
    }
    maven { url 'https://jitpack.io' }
}

dependencies {
    compileOnly 'com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT'
    compileOnly fileTree(dir: "./server/cache/", include: "patched_1.16.5.jar")
    compileOnly fileTree(dir: "./server/cache/", include: "NickAPI-v6.3.3.jar")
    compileOnly "com.comphenix.protocol:ProtocolLib:4.7.0"
    implementation 'dev.kotx:flylib-reloaded:0.5.0'
    implementation 'com.github.TeamKun:ConfigLib:0.7.0'
    compileOnly 'org.yaml:snakeyaml:1.17'
}

shadowJar {
    archiveFileName = "${rootProject.name}-${version}.jar"
}

tasks.build.dependsOn tasks.shadowJar

task copyToServer(type: Copy) {
    mustRunAfter build
    from new File(buildDir.absolutePath, "libs/${rootProject.name}-${version}.jar")
    into "./server/plugins"
}

task buildAndCopy(group: "build") {
    dependsOn build, copyToServer
}

processResources {
    filteringCharset "UTF-8"
    filesMatching("**/plugin.yml") {
        filter(ReplaceTokens, tokens: [
                "version": version,
        ])
    }
}